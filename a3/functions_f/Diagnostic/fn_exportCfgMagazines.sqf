/*
	Author: Karel Moricky

	Description:
	Export list of addons for Community Wiki
	http://community.bistudio.com/wiki/Category:Arma_3:_Assets

	Parameter(s):
		0: ARRAY of STRINGs - list of CfgPatches classes

	Returns:
	BOOL
*/


startloadingscreen [""];

_patches = [_this,1,[],[[]]] call bis_fnc_param;
_patches = +_patches;
{
	_patches set [_foreachindex,tolower _x];
} foreach _patches;
_allPatches = count _patches == 0;

_br = tostring [13,10];
_product = productversion select 0;
_productShort = productversion select 1;

_fnc_getItemPage = {
	switch (_this) do {
		case "Weapon": {"CfgWeapons Weapons"};
		case "VehicleWeapon": {"CfgWeapons Vehicle Weapons"};
		case "Item": {"CfgWeapons Items"};
		default {"CfgWeapons"};
	};
};

_magazineWeapons = [];
{
	_weaponCfg = _x;
	_weapon = configname _x;
	if (getnumber (_x >> "scope") > 0) then {
		_magazines = getarray (_x >> "magazines");
		{_magazines = _magazines + getarray (_weaponCfg >> _x >> "magazines")} foreach getarray (_x >> "muzzles");
		{
			_magazine = tolower _x;
			_magazineID = _magazineWeapons find _magazine;
			if (_magazineID < 0) then {
				_magazineID = count _magazineWeapons;
				_magazineWeapons set [_magazineID,_magazine];
				_magazineWeapons set [_magazineID + 1,[]];
			};
			_weapons = _magazineWeapons select (_magazineID + 1);
			if !(_magazine in _weapons) then {_weapons set [count _weapons,_weapon];};
		} foreach _magazines;
	};
} foreach ((configfile >> "cfgweapons") call bis_fnc_returnchildren);

_text = "{| class=""wikitable sortable"" border=""1"" style=""border-collapse:collapse; font-size:80%;"" cellpadding=""3px""" + _br;
_text = _text + "! Class<br />" + _br;
_text = _text + "! Name<br />" + _br;
_text = _text + "! Inventory description<br />" + _br;
_text = _text + "! Ammo<br />" + _br;
_text = _text + "! Used by<br />" + _br;

{
	_class = configname _x;
	if (_allPatches || (tolower _class) in _patches) then {
		_displayNameArray = toarray gettext (_x >> "displayName");
		{
			if (_x == 160) then {_displayNameArray set [_foreachindex,32];};
		} foreach _displayNameArray;
		_displayName = tostring _displayNameArray;

		_descriptionShortArray = toarray gettext (_x >> "descriptionShort");
		{
			if (_x == 160) then {_descriptionShortArray set [_foreachindex,32];};
		} foreach _descriptionShortArray;
		_descriptionShort = tostring _descriptionShortArray;

		_ammo = gettext (_x >> "ammo");
		//_textAmmo = if (_ammo != "") then {format ["[[%1 CfgAmmo#%2|%2]]",_product,_ammo]} else {""};

		_textWeapons = "";
		_magazineID = _magazineWeapons find (tolower _class);
		if (_magazineID >= 0) then {
			_weapons = _magazineWeapons select (_magazineID + 1);
			{
				_type = _x call bis_fnc_itemType;
				_page = (_type select 0) call _fnc_getItemPage;
				//_textWeapons = _textWeapons + _br + format [":[[%1 CfgWeapons %3#%2|%2]]",_product,_x,_textSide];
				_textWeapons = _textWeapons + _br + format [":[[%1 %3#%2|%2]]",_product,_x,_page];
			} foreach _weapons;
		};

		_text = _text + "|-" + _br;
		_text = _text + "| " + format ["<span id=""%1"" >'''%1'''</span>",_class] + _br;
		_text = _text + "| " + "''" + _displayName + "''" + _br;
		_text = _text + "| " + "''" + _descriptionShort + "''" + _br;
		_text = _text + "| " + _ammo + _br;
		_text = _text + "| " + _textWeapons + _br;
	};
} foreach ((configfile >> "CfgMagazines") call bis_fnc_returnchildren);

_text = _text + "|}" + _br;
_text = _text + format [
	"<small style=""color:grey;"">Generated by [[%1]] in [[%2]] version %3.%4 by ~~~~</small>",
	_fnc_scriptName,
	productversion select 0,
	(productversion select 2) * 0.01,
	productversion select 3
] + _br;
_text = _text + format ["{{Template:%1 Assets}}",_product] + _br;
_text = _text + format ["[[Category:%1: Assets]]",_product] + _br;
_text = _text + format ["[[Category:%1: Editing]]",_product] + _br;
copytoclipboard _text;

endloadingscreen;