/*
	Author: Karel Moricky

	Description:
	Prints config entries.

	Parameter(s):
		0: CONFIG - config class which will be searched (default: configFile)
		1: STRING or ARRAY - entry name(s) whose values will be printed
		2: STRING - scan mode, can be one of following:
			"controls" - recursively scans controls in root, class controls, class controlsBackground and in RscControlsGroup controls (type = 15).
			"turrets" - recursively scans vehicles's turrets.
			"weapons" - scans weapon muzzles and modes.
			(default) - scans just class roots.

	Returns:
	STRING

	Results are printed to debug log and wiki compatible table is copied to clipboard.
*/

startloadingscreen [""];

_cfg = [_this,0,configfile,[configfile]] call bis_fnc_param;
_entries = [_this,1,"",["",[],{}]] call bis_fnc_param;
_mode = [_this,2,"",[""]] call bis_fnc_param;
if (typename _entries != typename []) then {_entries = [_entries];};

_br = tostring [13,10];
_tab = "	";
_result = "Config" + _tab + "Class";

{
	if (typename _x == typename {}) then {_x = str _x;};
	_result = _result + _tab + _x;
} foreach _entries;

_fnc_print = {
	private ["_resultClass","_print"];
	_resultClass = [];
	_print = "";
	{
		_value = if (typename _x == typename {}) then {_this call _x} else {[_this,_x] call BIS_fnc_returnConfigEntry};
		if !(isnil "_value") then {
			_resultClass = _resultClass + [_value];
			_print = _print + " | " + str _x + ":	" + str _value;
		} else {
			_resultClass = _resultClass + [false];
		};
	} foreach _entries;
	if (count _resultClass > 0) then {
		(str _this + _print) call bis_fnc_log;

		//_result = _result + "|-" + _br + "| " + str _this + _br;
		_result = _result + _br + str _this + _tab + configname _this + _tab;
		
		{
			_resultText = switch (typename _x) do {
				case (typename false): {
					""
				};
				case (typename []): {
					_resultTextLocal = "=""""";
					_xCount = count _x - 1;
					{
						_resultTextLocal = if (_foreachindex == _xCount) then {
							_resultTextLocal + format ["&""%1""",_x];
						} else {
							_resultTextLocal + format ["&""%1,""",_x] + "&CHAR(10)";

						};
					} foreach _x;
					_resultTextLocal
				};
				default {str _x};
			};
			//_result = _result + "| " + _resultText + _br;
			_result = _result + _resultText + _tab;
		} foreach _resultClass;
	};
};

_fnc_explore = switch tolower _mode do {
	case "controls": {
		{
			private ["_control","_type"];
			_this call _fnc_print;
			{
				for "_n" from 0 to (count _x - 1) do {
					_control = _x select _n;
					if (isclass _control) then {
						_type = getnumber (_control >> "type");
						if (_type == 15) then {
							_control call _fnc_explore;
						} else {
							_control call _fnc_print;
						};
					};
				};
			} foreach [
				_this >> "controlsBackground",
				_this >> "controls"
			];
		};
	};
	case "turrets": {
		{
			private ["_turrets","_turret"];
			_this call _fnc_print;
			_turrets = _this >> "turrets";
			if (isclass _turrets) then {
				for "_t" from 0 to (count _turrets - 1) do {
					_turret = _turrets select _t;
					if (isclass _turret) then {
						_turret call _fnc_explore;
					};
				};
			};
		};
	};
	case "weapons": {
		{
			private ["_muzzles","_modes"];
			_this call _fnc_print;
			_muzzles = getarray (_this >> "muzzles");
			_modes = getarray (_this >> "modes");
			{
				(_this >> _x) call _fnc_explore;
			} foreach (_muzzles + _modes - ["this"]);
		};
	};
	default {
		{};
	};
};

_fnc_root = {
	private ["_class"];
	_class = _this;
	if (isclass _class) then {
		switch (tolower _mode) do {
			case "controls";
			case "turrets";
			case "weapons": {
				_class call _fnc_explore;
			};
			default {
				_class call _fnc_print;
			};
		};
	};
};

//--- Scan
_cfg call _fnc_root;
_cfgCount = count _cfg - 1;
for "_c" from 0 to (_cfgCount - 1) do {
	_class = _cfg select _c;
	_class call _fnc_root;

	progressloadingscreen (_c / _cfgCount);
};

//--- Wiki output
/*
_resultHeader = "<div style=""margin-bottom:-0.4em; font-size:75%; color:grey;"">Automatic output generated by [https://altar.bistudio.com/svn/futura/CA/Functions_F/Diagnostic/fn_diagConfig.sqf BIS_fnc_diagConfig]</div>" + _br;
_resultHeader = _resultHeader + "{| class=""wikitable sortable"" border=""1"" style=""border-collapse:collapse"" cellpadding=""3px""" + _br;
{
	_resultHeader = _resultHeader + "! style=""background:LightBlue;"" | " + _x + _br;
} foreach (["Class"] + _entries);
_result = _resultHeader + _result +_br + "|}";
*/
copytoclipboard _result;

endloadingscreen;

_result