/*
	Author: Karel Moricky

	Description:
	Export list of addons for Community Wiki
	http://community.bistudio.com/wiki/Category:Arma_3:_Assets

	Parameter(s):
		0: ARRAY of STRINGs - list of CfgPatches classes

	Returns:
	BOOL
*/


startloadingscreen [""];
_cfg = [_this,0,"cfgVehicles",[""]] call bis_fnc_param;

_patches = [_this,1,[],[[]]] call bis_fnc_param;
_patches = +_patches;
{
	_patches set [_foreachindex,tolower _x];
} foreach _patches;
_allPatches = count _patches == 0;

_br = tostring [13,10];
_product = productversion select 0;
_productShort = productversion select 1;

_fnc_getItemPage = {
	switch (_this) do {
		case "Weapon": {"CfgWeapons Weapons"};
		case "VehicleWeapon": {"CfgWeapons Vehicle Weapons"};
		case "Item": {"CfgWeapons Items"};
		case "Equipment": {"CfgWeapons Equipment"};
		default {"CfgWeapons"};
	};
};

_text = "{| class=""wikitable sortable"" border=""1"" style=""border-collapse:collapse; font-size:100%;"" cellpadding=""3px""" + _br;
_text = _text + "! Class<br />" + _br;
_text = _text + "! Objects<br />" + _br;
_text = _text + "! Objects Gallery<br />" + _br;

{
	_class = configname _x;
	if (_allPatches || (tolower _class) in _patches) then {
		_textAddons = "";
		_textUnits = "";
		_textWeapons = "";
		_textSide = "";
		_galleryUnits = "";
		_galleryWeapons = "";

		_units = getarray (_x >> "units");
		if (_cfg == "cfgVehicles" && count _units == 0) exitwith {};
		_weapons = getarray (_x >> "weapons");
		if (_cfg != "cfgVehicles" && count _weapons == 0) exitwith {};


		{
			_textSide = if (_x iskindof "allvehicles") then {
				str ((getnumber (configfile >> "cfgvehicles" >> _x >> "side") min 3) call bis_fnc_sidetype)
			} else {
				"EMPTY"
			};
			_textUnits = _textUnits + _br + format [":[[%1 CfgVehicles %4#%3|%3]]",_product,_productShort,_x,_textSide];
			_galleryUnits = _galleryUnits + format ["[[File:%2 CfgVehicles %3.jpg|150px|&nbsp;]]",_product,_productShort,_x,_textSide];
		} foreach _units;

		{
			if (isclass (configfile >> "cfgweapons" >> _x)) then {
				_type = _x call bis_fnc_itemType;
				_page = (_type select 0) call _fnc_getItemPage;
				_textWeapons = _textWeapons + _br + format [":[[%1 %4#%3|%3]]",_product,_productShort,_x,_page];
				_galleryWeapons = _galleryWeapons + format ["[[File:%2 CfgWeapons %3.jpg|150px|&nbsp;]]",_productShort,_productShort,_x];
			};
		} foreach _weapons;


		_text = _text + "|-" + _br;
		_text = _text + "| " + format ["<span id=""%1"" >'''%1'''</span>",_class] + _br;
		if (_cfg == "cfgVehicles") then {
			_text = _text + "| " + "<small>" + _textUnits + _br + "</small>" + _br;
			_text = _text + "| " + "<small>" + _galleryUnits + _br + "</small>" + _br;
		} else {
			_text = _text + "| " + "<small>" + _textWeapons + _br + "</small>" + _br;
			_text = _text + "| " + "<small>" + _galleryWeapons + _br + "</small>" + _br;
		};
	};
} foreach ((configfile >> "cfgpatches") call bis_fnc_returnchildren);

_text = _text + "|}" + _br;
_text = _text + format [
	"<small style=""color:grey;"">Generated by [[%1]] in [[%2]] version %3.%4 by ~~~~</small>",
	_fnc_scriptName,
	productversion select 0,
	(productversion select 2) * 0.01,
	productversion select 3
] + _br;
_text = _text + format ["{{Template:%1 Assets}}",_product] + _br;
_text = _text + format ["[[Category:%1: Assets]]",_product] + _br;
_text = _text + format ["[[Category:%1: Editing]]",_product] + _br;
copytoclipboard _text;

endloadingscreen;